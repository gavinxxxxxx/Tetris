apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'

def static autoVersionCode() {
    return Integer.parseInt(new Date().format("yyMMdd", TimeZone.getTimeZone("UTC")))
}

def static autoVersionName() {
    return new Date().format("0.yy.MM.dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "me.gavin.game.tetris"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode autoVersionCode()
        versionName autoVersionName()
        resConfigs "zh"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            versionNameSuffix "_debug"
            buildConfigField "boolean", "LOG_DEBUG", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            versionNameSuffix "_beta"
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

andResGuard {
    //mappingFile用于增量更新，保持本次混淆与上次混淆结果一致；
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    //uss7zip为true时，useSign必须为true；
    use7zip = false
    //useSign为true时，需要配置signConfig；
    useSign = true
    //打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字;
    keepRoot = false
    //whiteList添加在代码内部需要动态获取的资源id，不混淆这部分；
    whiteList = [
            // for your icon
            "R.mipmap.ic_launcher",
            "R.mipmap.ic_launcher_round",

            // "R.string.umeng*",
    ]
    //用来指定文件重打包时是否压缩指定文件;
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    //sevenzip可使用artifacr或path，path指本地安装的7za（7zip命令行工具）。
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.7'
        //path = "/usr/local/bin/7za"
    }
    // v2签名会使得7zip压缩失效，如果你对apk大小有极致的要求，可以在signingConfigs中关闭v2签名
    // gradlew resguardRelease

    configurations.all {
        // 强制指定版本解决多版本冲突问题
        // Fix@ Conflict with dependency 'com.android.support:support-annotations' in project ':app'.
        // @link {https://stackoverflow.com/questions/36561037/conflict-with-dependency-com-android-supportsupport-annotations-resolved-ver}
        // resolutionStrategy.force 'com.android.support:support-annotations:21.0.3'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.google.code.gson:gson:2.8.1'
}
